apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.example.mycomponentdemo"
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // 必须要保证所有的flavor都属于同一个维度
        flavorDimensions "default"
    }

    buildTypes {
        debug {
            // 关闭zipalign优化
            zipAlignEnabled = false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // 开启zipalign优化
            zipAlignEnabled = true
        }
    }

    productFlavors {
        car {
            // 这里可以添加car的打包配置
        }

        phone {
            // 这里可以添加phone的打包配置
        }

        pad {
            // 这里可以添加pad的打包配置
        }
    }

    // app签名
    signingConfigs {
        def appStoreFile = System.getenv("STORE_FILE")
        def appStorePassword = System.getenv("STORE_PASSWORD")
        def appKeyAlias = System.getenv("KEY_ALIAS")
        def appKeyPassword = System.getenv("KEY_PASSWORD")

        //当不能从环境变量里获取签名信息的时候，就使用项目中自带的debug签名
        if(!appStoreFile||!appStorePassword||!appKeyAlias||!appKeyPassword){
            appStoreFile = "debug.keystore"
            appStorePassword = "android"
            appKeyAlias = "androiddebugkey"
            appKeyPassword = "android"
        }
        release {
            storeFile file(appStoreFile)
            storePassword appStorePassword
            keyAlias appKeyAlias
            keyPassword appKeyPassword
        }
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        // 指定apk的输出路径
        //variant.getPackageApplication().outputDirectory = new File("./apk")
        // 指定apk输出的名称
        outputFileName = "${variant.name}_v${variant.versionName}.apk"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation rootProject.dependencie["appcompat"]
    implementation rootProject.dependencie["constraintlayout"]
    testImplementation rootProject.dependencie["junit"]
    androidTestImplementation rootProject.dependencie["testjunit"]
    androidTestImplementation rootProject.dependencie["espresso-core"]
    implementation project(path: ':function:common')
    if (isLibrary) {
        implementation project(path: ':business:maincomponent')
        implementation project(path: ':business:acomponent')
        implementation project(path: ':business:ccomponent')
        implementation project(path: ':business:dcomponent')
        implementation project(path: ':business:bcomponent')
    }
}